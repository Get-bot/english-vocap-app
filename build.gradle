import org.springframework.boot.gradle.plugin.SpringBootPlugin

// 루트 프로젝트 - 공통 설정 및 모듈 관리
plugins {
    id 'org.springframework.boot' version '3.5.3' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
}

// 공통 버전 정의
ext {
    querydslVersion = '5.0.0'
    jjwtVersion = '0.12.3'
}

// QueryDSL 설정 함수 - 필요한 모듈에서만 호출
ext.configureQuerydsl = { Project project ->
    def querydslDir = "src/main/generated"

    project.sourceSets {
        main {
            java {
                srcDirs += [querydslDir]
            }
        }
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.generatedSourceOutputDirectory = project.file(querydslDir)
    }

    project.clean {
        delete project.file(querydslDir)
    }
}

// 모든 서브프로젝트에 적용할 공통 설정
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.github.get-bot'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
            showStandardStreams = false
        }
    }
}
